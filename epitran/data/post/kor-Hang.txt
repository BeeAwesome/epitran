::initialconsonants:: = k|kʰ|k͈|n|t|t͈|ɾ|m|p|p͈|s|s͈|t͡ɕ|t͡ɕ͈|t͡ɕʰ|tʰ|pʰ|h
::vowels:: = i|e|ɛ|a|o|u|ʌ|ɯ|ɰi|je|jɛ|ja|jo|ju|jʌ|wi|we|wɛ|wa|wʌ
::nasals:: = n|m
::finalObs:: = k|t|p
::finalforNasal:: = k|t|m|p|ŋ
::jvowel:: = i|je|jɛ|ja|jo|ju|jʌ

% remove ㅇ if followed by a vowel (if it is in initial consonant position)
ŋ -> 0 / _ (::vowels::)

% change ɰi to i if preceded by consonant
ɰi -> i / (::initialconsonants::) _

% Substitution of lax to aspirated for consonant clusters with 'h'
nht -> ntʰ / (::vowels::) _ (::vowels::)
nhk -> nkʰ / (::vowels::) _ (::vowels::)
nht͡ɕ -> nt͡ɕʰ / (::vowels::) _ (::vowels::)
lht -> lhtʰ / (::vowels::) _ (::vowels::)
lhk -> lhkʰ / (::vowels::) _ (::vowels::)
lht͡ɕ -> lht͡ɕʰ / (::vowels::) _ (::vowels::)
lph -> lpʰ / (::vowels::) _ (::vowels::)
lkh -> lkʰ / (::vowels::) _ (::vowels::)
nt͡ɕh -> nt͡ɕʰ / (::vowels::) _ (::vowels::)

% Substitution of lax to tensce for consonant clusters with 's'
ks -> ks͈ / (::vowels::) _ (::vowels::)
ls -> ls͈ / (::vowels::) _ (::vowels::)
ps-> ps͈ / (::vowels::) _ (::vowels::)

% Simplification of consonant clusters to single consonants at coda 
palp -> pap / _ (::initialconsonants::)
nʌlp -> nʌp / _ (::initialconsonants::)
nh -> n / _ (::initialconsonants::|#)
nh -> n / (::vowels::) _ (::vowels::)
lh -> l / _ (::initialconsonants::|#)
lh -> l / (::vowels::) _ (::vowels::)
ks -> k / _ (::initialconsonants::|#)
nt͡ɕ -> n / _ (::initialconsonants::|#)
lp -> l / _ (::initialconsonants::|#)
ls -> l / _ (::initialconsonants::|#)
ltʰ -> l / _ (::initialconsonants::|#)
ps -> p / _ (::initialconsonants::|#)
lk -> k / _ (::initialconsonants::|#)
lm -> m / _ (::initialconsonants::|#)
lpʰ -> pʰ / _ (::initialconsonants::|#)

% Change consonants regarding phonological rule that limits the final consonants in a syllable to one of seven sounds
k͈ -> k / _ (::initialconsonants::|#)
kʰ -> k / _ (::initialconsonants::|#)
s -> t / _ (::initialconsonants::|#)
s͈ -> t / _ (::initialconsonants::|#)
t͡ɕ -> t / _ (::initialconsonants::|#)
t͡ɕʰ -> t / _ (::initialconsonants::|#)
tʰ -> t / _ (::initialconsonants::|#)
pʰ -> p / _ (::initialconsonants::|#)

% Substitute lax to aspirated if h precedes or follows 
ph -> pʰ / (::vowels::) _ (::vowels::)
hp -> pʰ / (::vowels::) _ (::vowels::)
th -> tʰ / (::vowels::) _ (::vowels::)
ht -> tʰ / (::vowels::) _ (::vowels::)
kh -> kʰ /(::vowels::) _ (::vowels::)
hk -> kʰ / (::vowels::) _ (::vowels::)

% Tensification 
k -> k͈ / (::finalObs::) _
t -> t͈ / (::finalObs::) _
p -> p͈ / (::finalObs::) _
s -> s͈ / (::finalObs::) _
t͡ɕ -> t͡ɕ͈ / (::finalObs::) _

% Nasaliztion 
l -> n / (::finalforNasal::) _ 
p -> m / _ (::nasals::)
t -> n / _ (::nasals::)
k -> ŋ / _ (::nasals::)

% Liquid
n -> l / l _
n -> l / _ l

% Palatalization
t -> t͡ɕ / _ (::jvowel::)
tʰ -> t͡ɕʰ / _(::jvowel::)